apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storeFile file(KEYSTORE_FILE)
            storePassword KEYSTORE_PASSWORD
        }
    }
    compileSdkVersion 30
    buildToolsVersion "30.0.2"


    defaultConfig {
        applicationId "ru.euphoria.doggy"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 204
        versionName "2.0.2 Beta"
    }

    dexOptions {
        preDexLibraries true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            zipAlignEnabled true
            shrinkResources true
        }

        debugMinify {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            zipAlignEnabled true
            shrinkResources true

            versionNameSuffix " Dev"
            applicationIdSuffix ".dev"

            ndk {
                // Building with NDK_DEBUG=1 for mips crashes the compiler in ndk 14.
                abiFilters 'arm64-v8a', 'x86'
            }
        }

        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ext.alwaysUpdateBuildId = false
            aaptOptions.cruncherEnabled = false
            splits.abi.enable = false
            splits.density.enable = false
            versionNameSuffix " Dev"
            applicationIdSuffix ".dev"

            ndk {
                // Building with NDK_DEBUG=1 for mips crashes the compiler in ndk 14.
                abiFilters 'arm64-v8a'
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        disable 'GoogleAppIndexingWarning'
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'

        exclude('META-INF/*.kotlin_module')
        exclude('**.kotlin_builtins')
        exclude('**.kotlin_metadata')
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'

    // Google Support
    implementation 'com.google.android.material:material:1.3.0-alpha02'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'
    implementation 'androidx.browser:browser:1.3.0-alpha06'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.android.billingclient:billing:1.1'

    // Exo Player
    implementation 'com.google.android.exoplayer:exoplayer-core:2.11.1'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.11.1'
    implementation 'com.google.android.exoplayer:extension-mediasession:2.11.1'

    // Ads
    implementation 'com.google.android.gms:play-services-ads:19.1.0'

    // Maps
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.maps.android:android-maps-utils:1.3.3'

    implementation 'com.google.android:flexbox:2.0.1'

    // Square Libraries
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.squareup.okhttp3:okhttp:4.2.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.1'
    implementation 'com.squareup.retrofit2:retrofit:2.7.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.17'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    // Room Persistent Library
    implementation "androidx.room:room-rxjava2:2.2.5"
    implementation "androidx.room:room-runtime:2.2.5"
    annotationProcessor "androidx.room:room-compiler:2.2.5"

    // Jake Wharton
    implementation 'com.jakewharton.rxbinding3:rxbinding:3.0.0-alpha2'
    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'

    implementation 'com.yandex.android:mobmetricalib:3.15.0'
    implementation 'org.jsoup:jsoup:1.12.1'
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.10.8'
    implementation 'de.siegmar:fastcsv:1.0.3'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.github.kbiakov:CodeView-Android:1.3.2'

}
